(* This model has been manually translated from
* Boolean network model of the control of the mammalian cell cycle from
* "Dynamical Analysis of a Generic Boolean Model for the Control of the 
* Mammalian Cell Cycle", A. Faure, A. Naldi, C. Chaouiya, D. Thieffry,
* Bioinformatics, 2006, vol. 22, no. 14, pp. e124-e131. 
 *)

"CycD" [0, 1]
"CycE" [0, 1]
"Rb" [0, 1]
"E2F" [0, 1]
"CycA" [0, 1]
"p27" [0, 1]
"Cdc20" [0, 1]
"UbcH10" [0, 1]
"Cdh1" [0, 1]
"CycB" [0, 1]

"CycE" 0 -> 1 when "Rb"=0 and "E2F"=1
"CycE" 1 -> 0 when "Rb"=1 
"CycE" 1 -> 0 when "E2F"=0

"Rb" 0 -> 1 when "CycD"=0 and "CycE"=0 and "CycA"=0 and "CycB"=0
"Rb" 0 -> 1 when "CycD"=0 and "p27"=1 and "CycB"=0
"Rb" 1 -> 0 when "CycA"=1 and "p27"=0 
"Rb" 1 -> 0 when "CycD"=1 
"Rb" 1 -> 0 when "CycB"=1

"E2F" 0 -> 1 when "Rb"=0 and "p27"=1 and "CycB"=0
"E2F" 0 -> 1 when "Rb"=0 and "CycA"=0 and "CycB"=0
"E2F" 1 -> 0 when "CycA"=1 and "p27"=0 
"E2F" 1 -> 0 when "Rb"=1
"E2F" 1 -> 0 when "CycB"=1 


"CycA" 0 -> 1 when "Rb"=0 and "E2F"=1 and "Cdc20"=0 and "Cdh1"=0
"CycA" 0 -> 1 when "Rb"=0 and "E2F"=1 and "Cdc20"=0 and "UbcH10"=0
"CycA" 1 -> 0 when "UbcH10"=1 and "Cdh1"=1
"CycA" 1 -> 0 when "Rb"=1
"CycA" 1 -> 0 when "Cdc20"=1


"p27" 0 -> 1 when "CycD"=0 and "CycE"=0 and "CycA"=0 and "CycB"=0
"p27" 1 -> 0 when "CycE"=1 and "CycA"=1
"p27" 1 -> 0 when "CycD"=1
"p27" 1 -> 0 when "CycB"=1

"Cdc20" 0 -> 1 when "CycB"=1
"Cdc20" 1 -> 0 when "CycB"=0


"UbcH10" 0 -> 1 when "Cdh1"=0
"UbcH10" 1 -> 0 when "CycA"=0 and "Cdc20"=0 and "Cdh1"=1 and "CycB"=0

"Cdh1" 0 -> 1 when "p27"=1 and "CycB"=0
"Cdh1" 0 -> 1 when "CycA"=0 and "CycB"=0
"Cdh1" 0 -> 1 when "Cdc20"=1
"Cdh1" 1 -> 0 when "Cdc20"=0 and "CycB"=1
"Cdh1" 1 -> 0 when "CycA"=1 and "p27"=0 and "Cdc20"=0 

"CycB" 0 -> 1 when "Cdc20"=0 and "Cdh1"=0
"CycB" 1 -> 0 when "Cdc20"=1
"CycB" 1 -> 0 when "Cdh1"=1 




